# Maintainer: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: khvalera ukr.net
# Contributor: Dmitry Nosachev quartz64 gmail
# Contributor: Patrick Schneider Patrick.Schneider uni-ulm.de

# Install guide:
# http://openmeetings.apache.org/installation.html
# The early steps are done by this PKGBUILD.

# Original instructions for 1.9.1, some of which may still apply:
# ==> Please make sure that mysql is reachable via TCP/IP
# ==> Do this by commenting out the following line in /etc/my.cnf:
# ==> skip-networking
#
# ==> OpenMeetings/red5 also requires you to open the following ports:
# ==> 5080 8443 1935 8088 9035"
#
# ==> Last, please configure your mysql database, user and password at
# ==> /opt/red5/webapps/openmeetings/WEB-INF/classes/META-INF/persistence.xml
# ==> Then start mysql and red5. If all went ok, you can access the
# ==> OpenMeetings installer at http://localhost:5080/openmeetings/install

pkgname=('openmeetings')
_pkgver='5.0.0-M4'
pkgver="${_pkgver//-/.}"
pkgver="${pkgver,,}"
pkgrel='4'
_rc=''
pkgdesc='Provides video conferencing, instant messaging, white board, collaborative document editing and other groupware tools. It uses API functions of Media Server for Remoting and Streaming (Red5 or Kurento)'
arch=('any')
url='http://openmeetings.apache.org/'
license=('apache')
depends=( 'libjpeg' 'lame' 'java-environment-openjdk=11')
optdepends=('imagemagick' 'ghostscript' 'sox' 'libreoffice' 'ffmpeg' 'mysql' 'openmeetings-mysql-connector-java' 'docker' 'kurento-media-server' 'kms-elements')
makedepends=('curl' 'gcc' 'make' 'libtool' 'bison' 'autoconf' 'automake' 'fakeroot' 'jshon' 'expac' 'mvnvm' 'java-environment-openjdk=11')
backup=("opt/openmeetings/webapps/openmeetings/WEB-INF/classes/META-INF/persistence.xml"
        "opt/openmeetings/webapps/openmeetings/WEB-INF/classes/org/apache/openmeetings/web/room/VideoSettings.html"
        "opt/openmeetings/conf/server.xml"
        "opt/openmeetings/conf/context.xml"
        "opt/openmeetings/conf/logging.properties")

# Nothing to strip in a Java package, wastes time!
options=('!strip')
install="${pkgname}.install"

source=("https://github.com/apache/openmeetings/archive/${_pkgver}${_rc}.tar.gz"
        "ba0d7f3.patch"
        "c46b7d8.patch"
        "cd45b78.patch")

sha256sums=('a6d218b807b8f9aee534a93d1dace74aac644cafd7e3ae54a35b267fd4c9c8e0'
            '02c1dc762b08cc17922adef941cbc57aecf2bd5d14c9db30ca0d82f772e124ff'
            '34fd0a434beffa712a0da0fec9cc152e146b33dfe63c4c46c6a58ab428a199e6'
            '3b1ea024c5b450b66c906cf476a78fe263609bcf9e24f8ea5573c9cc9f5a7ca7')

package() {
  # Use a temporary local maven repository.
  local mvn_repo="${srcdir}/mvn-repository"

  cd "${srcdir}/openmeetings-${_pkgver}${_rc}"

  # fix: Error while opening encoder for output stream #0:0
  patch -p1 <"$srcdir/ba0d7f3.patch"

  # fix: Sending messages through an external mailbox
  patch -p1 <"$srcdir/c46b7d8.patch"
  patch -p1 <"$srcdir/cd45b78.patch"

  export JAVA_HOME="/usr/lib/jvm/java-11-openjdk"
  mvn clean install -Dmaven.repo.local="${mvn_repo}" -PallModules

  install -dm755 ${pkgdir}/opt/openmeetings/webapps/openmeetings/{docs,screenshare}
  install -dm755 ${pkgdir}/run/openmeetings

  cp -p -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/target/apache-tomcat-9.0.34/* \
           ${pkgdir}/opt/openmeetings

  cp -f -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/src/main/assembly/conf/* \
           ${pkgdir}/opt/openmeetings/conf

  rm ${pkgdir}/opt/openmeetings/bin/{*.bat,*.tar.gz}
  rm ${pkgdir}/opt/openmeetings/conf/tomcat-users.*

  cp -p -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/src/main/assembly/scripts/*.sh \
           ${pkgdir}/opt/openmeetings/bin

  install -Dpm644 "${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/src/main/assembly/scripts/openmeetings.service" \
          -t "${pkgdir}/usr/lib/systemd/system/"

  install -d "${pkgdir}/run/openmeetings"

  find "${pkgdir}/opt/openmeetings" -type 'f' '(' -name '*.bat' -o -name '*.tar.gz' ')' -delete

  find "${pkgdir}/opt/openmeetings/bin" -type 'f' -name '*.sh' -exec chmod 755 '{}' '+'

  # Fix openmeetings.service
  sed -Ee 's:^(Environment=JAVA_HOME)=.*$:\1=/usr/lib/jvm/java-11-openjdk:g' \
      -e 's:^(Group)=.*$:\1=nobody:g' \
      -e 's:^(Environment=CATALINA_PID)=.*$:\1=/var/run/openmeetings/openmeetings.pid:g' \
      -i "${pkgdir}/usr/lib/systemd/system/openmeetings.service"

  rm -R ${pkgdir}/opt/openmeetings/webapps/{examples,host-manager,manager,docs,ROOT}

  # install webapps openmeetings
  cp -p -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-web/target/openmeetings-web-5.0.0-M4/* \
           ${pkgdir}/opt/openmeetings/webapps/openmeetings

  # install webapps ROOT
  cp -p -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/src/main/assembly/ROOT \
           ${pkgdir}/opt/openmeetings/webapps

  # install webapps docs
  cp -p -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/target/site/* \
           ${pkgdir}/opt/openmeetings/webapps/openmeetings/docs

  # install screenshare
  cp -f -r ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-screenshare/target/jnlp/* \
           ${pkgdir}/opt/openmeetings/webapps/openmeetings/screenshare

  # localhost.jks - later need to fix
  tar -xz -C ${srcdir}/openmeetings-${_pkgver}${_rc} -f  \
             ${srcdir}/openmeetings-${_pkgver}${_rc}/openmeetings-server/target/apache-openmeetings-${_pkgver}${_rc}.tar.gz

  cp -f ${srcdir}/openmeetings-${_pkgver}${_rc}/apache-openmeetings-${_pkgver}${_rc}/conf/localhost.jks \
        ${pkgdir}/opt/openmeetings/conf/localhost.jks

}
